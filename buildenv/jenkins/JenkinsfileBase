#!groovy
def getBuildList() {
	// This map is created in case of BUILD_LIST is not provided. Once we match all sub-folders with top-level test targets, this map can be simplified.
	def TESTPROJECTS = [system:'system', perf:'perf', jck:'jck', external:'external', functional: 'functional', openjdk:'openjdk', jdk:'openjdk', runtest:'', sanity:'', extended:'']
	String fullTarget="${TARGET}"
	String[] levelTargets = fullTarget.split('\\.')
	String groupTarget = levelTargets[-1]
	String[] partsTarget = groupTarget.split('_|-')
	String simpleTarget = partsTarget[0]
	def TESTPROJECT = TESTPROJECTS[simpleTarget]
	return TESTPROJECT
}

def makeTest(testParam) {
	String tearDownCmd = "if [ `uname` = AIX ] || [ `uname` = SunOS ]; then MAKE=gmake; else MAKE=make; fi; \$MAKE -f ./openjdk-tests/TKG/testEnv.mk testEnvTeardown"
	try {
		sh "$tearDownCmd"
		sh "./openjdk-tests/maketest.sh $testParam"
	} finally {
		sh "$tearDownCmd"
	}
}

def setupEnv() {
	if ( params.JDK_VERSION ) {
		env.ORIGIN_JDK_VERSION = params.JDK_VERSION
		env.JDK_VERSION = params.JDK_VERSION.equalsIgnoreCase("next") ? "" : params.JDK_VERSION
	}

	env.SPEC = "${SPEC}"

	PLATFORM = params.PLATFORM ? params.PLATFORM : ""
	ADOPTOPENJDK_REPO = params.ADOPTOPENJDK_REPO ? params.ADOPTOPENJDK_REPO : (env.SPEC.startsWith('zos') ? "git@github.com:AdoptOpenJDK/openjdk-tests.git" : "https://github.com/AdoptOpenJDK/openjdk-tests.git")
	ADOPTOPENJDK_BRANCH = params.ADOPTOPENJDK_BRANCH ? params.ADOPTOPENJDK_BRANCH : "master"
	CLONE_OPENJ9 = params.CLONE_OPENJ9 ? params.CLONE_OPENJ9 : "true"
	OPENJ9_REPO = params.OPENJ9_REPO ? params.OPENJ9_REPO : (env.SPEC.startsWith('zos') ? "git@github.com:eclipse/openj9.git" : "https://github.com/eclipse/openj9.git")
	OPENJ9_BRANCH = params.OPENJ9_BRANCH ? params.OPENJ9_BRANCH : "master"
	CUSTOM_TARGET = params.CUSTOM_TARGET ? params.CUSTOM_TARGET : ""
	TKG_REPO = params.TKG_REPO ? params.TKG_REPO : (env.SPEC.startsWith('zos') ? "git@github.com:AdoptOpenJDK/TKG.git" :"https://github.com/AdoptOpenJDK/TKG.git")
	TKG_BRANCH = params.TKG_BRANCH ? params.TKG_BRANCH : "master"
	TKG_SHA = params.TKG_SHA ? params.TKG_SHA : ""
	UPSTREAM_JOB_NAME = params.UPSTREAM_JOB_NAME ? params.UPSTREAM_JOB_NAME : ""
	UPSTREAM_JOB_NUMBER = params.UPSTREAM_JOB_NUMBER ? params.UPSTREAM_JOB_NUMBER : ""
	SSH_AGENT_CREDENTIAL = params.SSH_AGENT_CREDENTIAL ? params.SSH_AGENT_CREDENTIAL : ""
	KEEP_WORKSPACE = params.KEEP_WORKSPACE ? params.KEEP_WORKSPACE : false
	OPENJ9_SHA = params.OPENJ9_SHA ? params.OPENJ9_SHA : ""
	env.USER_CREDENTIALS_ID = params.USER_CREDENTIALS_ID ? params.USER_CREDENTIALS_ID : ""
	env.TEST_JDK_HOME = "$WORKSPACE/openjdkbinary/j2sdk-image"
	env.JVM_VERSION = params.JVM_VERSION ? params.JVM_VERSION : ""
	env.JVM_OPTIONS = params.JVM_OPTIONS ? params.JVM_OPTIONS : ""
	env.EXTRA_OPTIONS = params.EXTRA_OPTIONS ? params.EXTRA_OPTIONS : ""
	env.EXTRA_DOCKER_ARGS = params.EXTRA_DOCKER_ARGS ? params.EXTRA_DOCKER_ARGS : ""

	env.TEST_FLAG = params.TEST_FLAG ? params.TEST_FLAG : ''
	env.KEEP_REPORTDIR = params.KEEP_REPORTDIR ? params.KEEP_REPORTDIR : ''
	SDK_RESOURCE = params.SDK_RESOURCE ? params.SDK_RESOURCE : "upstream"
	env.AUTO_DETECT = params.AUTO_DETECT

	ITERATIONS = params.ITERATIONS ? "${params.ITERATIONS}".toInteger() : 1

	if (params.JRE_IMAGE) {
		env.JRE_IMAGE = "${WORKSPACE}/${params.JRE_IMAGE}"
	}

	if (params.JDK_IMPL) {
		env.JDK_IMPL = params.JDK_IMPL
	} else if (params.JVM_VERSION) {
		env.JDK_IMPL = getJDKImpl(params.JVM_VERSION)
	}
	if( params.BUILD_LIST ) {
		env.BUILD_LIST = params.BUILD_LIST
	} else {
		env.BUILD_LIST = "${getBuildList()}"
	}

	if( params.PERF_ROOT ) {
		env.PERF_ROOT = params.PERF_ROOT
	} else {
		env.PERF_ROOT = "$WORKSPACE/../../benchmarks"
	}

	env.JCK_VERSION = params.JCK_VERSION ? params.JCK_VERSION : ""
	env.JCK_ROOT = params.JCK_ROOT ? params.JCK_ROOT : ""
	env.JCK_GIT_REPO = params.JCK_GIT_REPO ? params.JCK_GIT_REPO : ""

	if (env.BUILD_LIST == 'openjdk' ||  env.BUILD_LIST.contains('external')) {
		env.DIAGNOSTICLEVEL ='noDetails'
	}

	// Optional parameter RELEASE_TAG and it is only used in openjdk regression test
	if( params.RELEASE_TAG ) {
		env.RELEASE_TAG = params.RELEASE_TAG
	}
	// Optional parameter RELEASE_TAG and it is only used in openjdk regression test
	if( params.OPENJDK_SHA) {
		env.OPENJDK_SHA = params.OPENJDK_SHA
	}
	if( params.DOCKERIMAGE_TAG ) {
		env.DOCKERIMAGE_TAG = params.DOCKERIMAGE_TAG
	}
	if ( env.SPEC.contains('sparcv')) {
		sh 'env'
	} else {
		sh 'printenv'
	}
}

def setupParallelEnv() {
	stage('setupParallelEnv') {
		timestamps{
			def testSubDirs = []
			int childJobNum = 1
			def UPSTREAM_TEST_JOB_NAME = ""
			def UPSTREAM_TEST_JOB_NUMBER = ""

			if (params.PARALLEL == "Iteration") {
				childJobNum = ITERATIONS
				// limit childJobNum
				if (childJobNum > 20) {
					echo "Due to the limited machines, ITERATIONS can only be set up to 20. Current ITERATIONS is ${ITERATIONS}."
					echo "Reset ITERATIONS to 20..."
					childJobNum = 20
				}
			} else if (params.PARALLEL == "Dynamic") {
				int numOfMachines = getNumMachines()
				String PARALLEL_OPTIONS = "TEST=${TARGET} NUM_MACHINES=${numOfMachines}"
				if (params.TRSS_URL) {
					PARALLEL_OPTIONS += " TRSS_URL=${params.TRSS_URL}"
				}
				sh "cd ./openjdk-tests/TKG; make genParallelList ${PARALLEL_OPTIONS}"

				// get NUM_LIST from parallelList.mk. NUM_LIST can be different than numOfMachines
				def parallelList = "openjdk-tests/TKG/parallelList.mk"
				int NUM_LIST = -1
				if (fileExists("${parallelList}")) {
					echo "read parallelList.mk file: ${parallelList}"
					def properties = readProperties file: "${parallelList}"
					if (properties.NUM_LIST) {
						NUM_LIST = properties.NUM_LIST.toInteger()
					}
				}
				if ( NUM_LIST > 0) {
					childJobNum = NUM_LIST
					echo "Saving parallelList.mk file on jenkins..."
					dir('openjdk-tests/TKG') {
						archiveArtifacts artifacts: 'parallelList.mk', fingerprint: true, allowEmptyArchive: false
					}

					UPSTREAM_TEST_JOB_NAME = JOB_NAME
					UPSTREAM_TEST_JOB_NUMBER = BUILD_NUMBER
				} else {
					assert false : "Build failed because cannot find NUM_LIST in parallelList.mk file."
				}
			} else if (params.PARALLEL == "Subdir") {
				dir("$WORKSPACE/openjdk-tests/${env.BUILD_LIST}") {
					testSubDirs = sh(returnStdout: true, script: "ls -d */").trim().tokenize()
				}

				if (TARGET.contains('special.system')) {
					// In special.system, some subfolders do not have any system test in special level
					// In order to save machine resources, exclude the following system test subfolders in parallel mode
					def excludes = ["jlm/", "modularity/", "sharedClasses/"]
					echo "exclude the following system test subfolders: ${excludes}"
					testSubDirs = testSubDirs - excludes
				}

				childJobNum = testSubDirs.size()
				if ( childJobNum > 20) {
					assert false : "Build failed becuase childJobNum: ${childJobNum} > 20."
				}
			}
			echo "[PARALLEL: ${params.PARALLEL}] childJobNum is ${childJobNum}, creating jobs and running them in parallel..."
			parallel_tests = [:]
			create_jobs = [:]

			for (int i = 0; i < childJobNum; i++) {
				def buildListName = env.BUILD_LIST
				def childTest = ""
				def childTarget = TARGET
				if (params.PARALLEL == "Iteration") {
					childTest = "iteration_${i}"
				} else if (params.PARALLEL == "Dynamic") {
					childTest = "testList_${i}"
					childTarget = "-f parallelList.mk ${childTest}"
				} else if (params.PARALLEL == "Subdir") {
					childTest = testSubDirs[i].trim().replace("/","");
					buildListName = "${env.BUILD_LIST}/${childTest}"
				}

				def TEST_JOB_NAME = "${JOB_NAME}_${childTest}"
				create_jobs[childTest] = {
					echo "create the job ${TEST_JOB_NAME}"
					createJob( TEST_JOB_NAME, PLATFORM)
				}

				def childParams = []
				// loop through all the params and change the parameters if needed
				params.each { param ->
					// set ITERATIONS, PARALLEL, and NUM_MACHINES to default value
					if (param.key == "ITERATIONS") {
						childParams << string(name: param.key, value: "1")
					} else if (param.key == "BUILD_LIST") {
						childParams << string(name: param.key, value: "${buildListName}")
					} else if (param.key == "TARGET") {
						childParams << string(name: param.key, value: "${childTarget}")
					} else if (param.key == "PARALLEL") {
						childParams << string(name: param.key, value: "None")
					} else if (param.key == "NUM_MACHINES") {
						childParams << string(name: param.key, value: "1")
					} else {
						def value = param.value.toString()
						if (value == "true" || value == "false") {
							childParams << booleanParam(name: param.key, value: value.toBoolean())
						} else {
							childParams << string(name: param.key, value: value)
						}
					}
				}

				childParams << string(name: 'UPSTREAM_TEST_JOB_NAME', value: UPSTREAM_TEST_JOB_NAME)
				childParams << string(name: 'UPSTREAM_TEST_JOB_NUMBER', value: UPSTREAM_TEST_JOB_NUMBER)

				//This parameter is needed because it enables many iterations of the same test (which all have same names)
				//to have a unique variable in the array so that Jenkins will run it
				childParams << string(name: 'RUNTIME_NAME', value: childTest)

				parallel_tests[childTest] = {
					build job: TEST_JOB_NAME, parameters: childParams
				}
			}
			parallel create_jobs

			// return to top level pipeline file in order to exit node block before running tests in parallel
		}
	}
}


// Returns num
// num = params.NUM_MACHINES. If it is not provided, the default value is 1
// num cannot be greater than slave machines / 2
def getNumMachines(){
	int num = params.NUM_MACHINES ? params.NUM_MACHINES.toInteger() : 1
	def slaves = nodesByLabel(LABEL).size()
	int limit = slaves / 2
	if (num > limit) {
		echo "Number of slave machines is ${slaves}. Number of machines cannot be greater than ${slaves} / 2 (=${limit}). Set num to ${limit}"
		num = limit
	}
	return num
}

def createJob( TEST_JOB_NAME, ARCH_OS ) {

	def jobParams = [:]
	jobParams.put('TEST_JOB_NAME', TEST_JOB_NAME)
	jobParams.put('ARCH_OS_LIST', ARCH_OS)

	//get level and group if TEST_JOB_NAME matches the format of Test_openjdk11_j9_extended.functional_ppc64_aix
	//otherwise, use default level and group value in template
	if (TEST_JOB_NAME.startsWith("Test_openjdk")) {
		def level = ""
		def group = ""
		def tokens = TEST_JOB_NAME.split('_');
		if (tokens.size() > 3 && tokens[3].contains(".")) {
			level = tokens[3].split("\\.")[0]
			group = tokens[3].split("\\.")[1]
		}

		if (level && group) {
			jobParams.put('LEVELS', level)
			jobParams.put('GROUPS', group)
		}
	}

	def templatePath = 'openjdk-tests/buildenv/jenkins/testJobTemplate'

	def create = jobDsl targets: templatePath, ignoreExisting: false, additionalParameters: jobParams
	return create
}

def setup() {
	stage('Setup') {
		timestamps{
			setupEnv()

			if (fileExists('openjdkbinary')) {
				dir('openjdkbinary') {
					deleteDir()
				}
			}
			if (fileExists('jvmtest')) {
				dir('jvmtest') {
					deleteDir()
				}
			}

			if (params.CUSTOMIZED_SDK_URL) {
				SDK_RESOURCE = "customized"
				CUSTOMIZED_SDK_URL_OPTION = "-c '${params.CUSTOMIZED_SDK_URL}'"
			} else {
				CUSTOMIZED_SDK_URL_OPTION = ""
			}
			if (params.CUSTOMIZED_SDK_SOURCE_URL) {
				SDK_RESOURCE = "customized"
				CUSTOMIZED_SDK_SOURCE_URL_OPTION = "-S '${params.CUSTOMIZED_SDK_SOURCE_URL}'"
			} else {
				CUSTOMIZED_SDK_SOURCE_URL_OPTION = ""
			}

			if (SDK_RESOURCE == 'upstream' && !params.CUSTOMIZED_SDK_URL) {
				dir('openjdkbinary') {
					step([$class: 'CopyArtifact',
						fingerprintArtifacts: true,
						flatten: true,
						filter: "**/*.tar.gz,**/*.tgz,**/*.zip,**/*.jar,**/*.Z",
						projectName: "${params.UPSTREAM_JOB_NAME}",
						selector: [$class: 'SpecificBuildSelector', buildNumber: "${params.UPSTREAM_JOB_NUMBER}"]])
				}
			}
			CLONE_OPENJ9_OPTION = (params.CLONE_OPENJ9) ? "--clone_openj9 ${params.CLONE_OPENJ9}" : ""
			OPENJ9_REPO_OPTION = "--openj9_repo ${OPENJ9_REPO}"
			OPENJ9_BRANCH_OPTION = "--openj9_branch ${OPENJ9_BRANCH}"
			OPENJ9_SHA_OPTION = (params.OPENJ9_SHA) ? "--openj9_sha ${params.OPENJ9_SHA}" : ""
			TKG_REPO_OPTION = "--tkg_repo ${TKG_REPO}"
			TKG_BRANCH_OPTION = "--tkg_branch ${TKG_BRANCH}"
			TKG_SHA_OPTION = (params.TKG_SHA) ? "--tkg_sha ${params.TKG_SHA}" : ""
			JDK_VERSION_OPTION = env.JDK_VERSION ? "-j ${env.JDK_VERSION}" : ""
			JDK_IMPL_OPTION = env.JDK_IMPL ? "-i ${env.JDK_IMPL}" : ""
			// system test repository exports to be used by system/common.xml
			if (params.ADOPTOPENJDK_SYSTEMTEST_REPO) {
				env.ADOPTOPENJDK_SYSTEMTEST_REPO = params.ADOPTOPENJDK_SYSTEMTEST_REPO
			}

			if (params.ADOPTOPENJDK_SYSTEMTEST_BRANCH) {
				env.ADOPTOPENJDK_SYSTEMTEST_BRANCH = params.ADOPTOPENJDK_SYSTEMTEST_BRANCH
			}

			if (params.OPENJ9_SYSTEMTEST_REPO) {
				env.OPENJ9_SYSTEMTEST_REPO = params.OPENJ9_SYSTEMTEST_REPO
			}

			if (params.OPENJ9_SYSTEMTEST_BRANCH) {
				env.OPENJ9_SYSTEMTEST_BRANCH = params.OPENJ9_SYSTEMTEST_BRANCH
			}

			if (params.STF_REPO) {
				env.STF_REPO = params.STF_REPO
			}

			if (params.STF_BRANCH) {
				env.STF_BRANCH = params.STF_BRANCH
			}

			// vendor test
			// expect VENDOR_TEST_* to be comma separated string parameters
			VENDOR_TEST_REPOS = (params.VENDOR_TEST_REPOS) ? "--vendor_repos \"${params.VENDOR_TEST_REPOS}\"" : ""
			VENDOR_TEST_BRANCHES = (params.VENDOR_TEST_BRANCHES) ? "--vendor_branches \"${params.VENDOR_TEST_BRANCHES}\"" : ""
			VENDOR_TEST_DIRS = (params.VENDOR_TEST_DIRS) ? "--vendor_dirs \"${params.VENDOR_TEST_DIRS}\"" : ""
			VENDOR_TEST_SHAS = (params.VENDOR_TEST_SHAS) ? "--vendor_shas \"${params.VENDOR_TEST_SHAS}\"" : ""

			// handle three cases (true/false/null) in params.TEST_IMAGES_REQUIRED and params.DEBUG_IMAGES_REQUIRED
			// Only set image required to false if params is set to false. In get.sh, the default value is true
			TEST_IMAGES_REQUIRED = (params.TEST_IMAGES_REQUIRED == false) ? "--test_images_required false" : ""
			DEBUG_IMAGES_REQUIRED = (params.DEBUG_IMAGES_REQUIRED == false) ? "--debug_images_required false" : ""
			GET_SH_CMD = "./openjdk-tests/get.sh -s `pwd` -t `pwd`/openjdk-tests -p $PLATFORM -r ${SDK_RESOURCE} ${JDK_VERSION_OPTION} ${JDK_IMPL_OPTION} ${CUSTOMIZED_SDK_URL_OPTION} ${CUSTOMIZED_SDK_SOURCE_URL_OPTION} ${CLONE_OPENJ9_OPTION} ${OPENJ9_REPO_OPTION} ${OPENJ9_BRANCH_OPTION} ${OPENJ9_SHA_OPTION} ${TKG_REPO_OPTION} ${TKG_BRANCH_OPTION} ${TKG_SHA_OPTION} ${VENDOR_TEST_REPOS} ${VENDOR_TEST_BRANCHES} ${VENDOR_TEST_DIRS} ${VENDOR_TEST_SHAS} ${TEST_IMAGES_REQUIRED} ${DEBUG_IMAGES_REQUIRED}"

			dir( WORKSPACE) {
				// use sshagent with Jenkins credentials ID for all platforms except zOS
				// on zOS use the user's ssh key
				if (!env.SPEC.startsWith('zos')) {
					get_sources_with_authentication()
				} else {
					get_sources()
				}
				getJobProperties()
			}
		}
	}
}

def getJobProperties() {
	def jobProperties = "./openjdk-tests/job.properties"
	if (fileExists("${jobProperties}")) {
		echo "readProperties file: ${jobProperties}"
		def properties = readProperties file: "${jobProperties}"
		if (properties.TEST_JDK_HOME) {
			env.TEST_JDK_HOME = properties.TEST_JDK_HOME
			echo "Reset TEST_JDK_HOME to ${TEST_JDK_HOME}"
		}
	}
}

def get_sources_with_authentication() {
	sshagent(credentials:["${params.USER_CREDENTIALS_ID}"], ignoreMissing: true) {
		get_sources()
	}
}

def get_sources() {
	withEnv(['VERBOSE_CURL=SILENT']) {
		if (params.CUSTOMIZED_SDK_URL_CREDENTIAL_ID) {
			// USERNAME and PASSWORD reference with a withCredentials block will not be visible within job output
			withCredentials([usernamePassword(credentialsId: "${params.CUSTOMIZED_SDK_URL_CREDENTIAL_ID}", usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')]) {
				sh "$GET_SH_CMD"
			}
		} else {
			sh "$GET_SH_CMD"
		}
	}
}

def make_test() {
	// use sshagent with Jenkins credentials ID for all platforms except zOS
	if (!env.SPEC.startsWith('zos')) {
		sshagent (credentials: ["$params.SSH_AGENT_CREDENTIAL"], ignoreMissing: true) {
			sh "./openjdk-tests/maketest.sh ./openjdk-tests";
		}
	} else {
		sh "./openjdk-tests/maketest.sh ./openjdk-tests";
	}
}

def buildTest() {
	stage('Build') {
		timestamps{
			echo 'Building tests...'

			if ( params.PERF_CREDENTIALS_ID ) {
				withCredentials([usernamePassword(credentialsId: "$params.PERF_CREDENTIALS_ID",
					passwordVariable: "PASSWORD_VAR", usernameVariable: "USERNAME_VAR")]) {
						env.PERF_USERNAME = USERNAME_VAR
						env.PERF_PASSWORD = PASSWORD_VAR
				}
			}

			if (params.UPSTREAM_TEST_JOB_NAME && params.UPSTREAM_TEST_JOB_NUMBER) {
				try {
					copyArtifacts fingerprintArtifacts: true, projectName: params.UPSTREAM_TEST_JOB_NAME, selector: specific(params.UPSTREAM_TEST_JOB_NUMBER), target: './openjdk-tests/TKG/'
				} catch (Exception e) {
					echo "Cannot run copyArtifacts from ${params.UPSTREAM_TEST_JOB_NAME} ${params.UPSTREAM_TEST_JOB_NUMBER}. Skipping copyArtifacts..."
				}
			}

			try {
				//get pre-staged jars from test.getDependency build before test compilation
				copyArtifacts fingerprintArtifacts: true, projectName: "test.getDependency", selector: lastSuccessful(), target: 'openjdk-tests/TKG/lib'
			} catch (Exception e) {
				echo 'Cannot run copyArtifacts from test.getDependency. Skipping copyArtifacts...'
			}

			try {
				if (env.BUILD_LIST.startsWith('system') || env.BUILD_LIST.startsWith('jck')) {
					//get pre-staged test jars from systemtest.getDependency build before system test compilation
					copyArtifacts fingerprintArtifacts: true, projectName: "systemtest.getDependency", selector: lastSuccessful(), target: 'openjdk-tests'
				}
			} catch (Exception e) {
				echo 'Cannot run copyArtifacts from systemtest.getDependency. Skipping copyArtifacts...'
			}

			if (fileExists('openjdkbinary/openjdk-test-image')) {
				env.TESTIMAGE_PATH = "$WORKSPACE/openjdkbinary/openjdk-test-image"
			}

			if (fileExists('openjdkbinary/openjdk-test-image/openj9')) {
				env.NATIVE_TEST_LIBS = "$WORKSPACE/openjdkbinary/openjdk-test-image/openj9"
			}

			if(params.CUSTOMIZED_SDK_URL_CREDENTIAL_ID) {
				withCredentials([usernamePassword(credentialsId: "${params.CUSTOMIZED_SDK_URL_CREDENTIAL_ID}",
					usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')]) {
					make_test()
				}
			} else {
				make_test()
			}
		}
	}
}

def runTest( ) {
	stage('Test') {
		timestamps{
			echo 'Running tests...'
			def CUSTOM_OPTION = ''
			int iterationCount = 1

			TARGET = "${params.TARGET}";
			if (TARGET.contains('custom') && CUSTOM_TARGET!='') {
				CUSTOM_OPTION = "${TARGET.toUpperCase()}_TARGET='${CUSTOM_TARGET}'"
			}
			if (!TARGET.startsWith('-f')) {
				TARGET="_${params.TARGET}"
			}
			RUNTEST_CMD = "./openjdk-tests ${TARGET} ${CUSTOM_OPTION}"
			for (int i = 0; i < ITERATIONS; i++) {
				echo "ITERATION: ${iterationCount}/${ITERATIONS}"
				if (env.BUILD_LIST == 'openjdk') {
					if (env.SPEC.startsWith('linux_x86-64')) {
						wrap([$class: 'Xvfb', autoDisplayName: true]) {
							def DISPLAY = sh (
								script: 'ps -f  | grep \'[X]vfb\' | awk \'{print \$9}\'',
								returnStdout: true
							).trim()
							env.DISPLAY = "${DISPLAY}"
							echo "env.DISPLAY is ${env.DISPLAY}"
							makeTest("${RUNTEST_CMD}")
						}
					} else {
						makeTest("${RUNTEST_CMD}")
					}
				} else if (env.BUILD_LIST.startsWith('jck')) {
					if (env.SPEC.startsWith('linux')) {
						wrap([$class: 'Xvfb', autoDisplayName: true]) {
							def DISPLAY = sh (
								script: 'ps -f  | grep \'[X]vfb\' | awk \'{print \$9}\'',
								returnStdout: true
							).trim()
							env.DISPLAY = "${DISPLAY}"
							echo "env.DISPLAY is ${env.DISPLAY}"
							makeTest("${RUNTEST_CMD}")
						}
					} else if (env.SPEC.startsWith('aix')) {
						sh "nohup /usr/bin/X11/X -force -vfb -x abx -x dbe -x GLX :0 &"
						env.DISPLAY = ":0"
						echo "env.DISPLAY is ${env.DISPLAY}"
						makeTest("${RUNTEST_CMD}")
					} else {
						makeTest("${RUNTEST_CMD}")
					}
				} else {
					makeTest("${RUNTEST_CMD}")
				}
				iterationCount++
			}
		}
	}
}

def post(output_name) {
	stage('Post') {
		timestamps{
			if (output_name.contains(',')) {
				output_name = "specifiedTarget"
			}
			else {
				output_name = output_name.replace("/","_")
			}
			def tar_cmd = "tar -cf"
			// Use pigz if we can as it is faster - 2> hides fallback message
			def tar_cmd_suffix = "| (pigz -9 2>/dev/null || gzip -9)"
			def suffix = ".tar.gz"
			def pax_opt = ""
			if (SPEC.startsWith('zos')) {
				echo 'Converting tap file from ebcdic to ascii...'
				sh 'cd ./openjdk-tests/TKG'
				def tapFiles = findFiles(glob: "**/*.tap")
				for (String tapFile : tapFiles) {
					sh "iconv -f ibm-1047 -t iso8859-1 ${tapFile} > ${tapFile}.ascii; rm ${tapFile}; mv ${tapFile}.ascii ${tapFile}"
				}

				tar_cmd = "pax -wf"
				suffix = ".pax.Z"
				pax_opt = "-x pax"
				tar_cmd_suffix = ""
			}

			boolean failIfNoResultsValue = true
			if (params.PARALLEL && params.PARALLEL != "None") {
				failIfNoResultsValue = false
			}
			step([$class: "TapPublisher", testResults: "**/*.tap", outputTapToConsole: false, failIfNoResults: failIfNoResultsValue])

			junit allowEmptyResults: true, keepLongStdio: true, testResults: '**/work/**/*.jtr.xml, **/junitreports/**/*.xml, **/external_test_reports/**/*.xml'
			archiveSHAFile()

			addGrinderLink()

			if (env.BUILD_LIST.startsWith('jck')) {
				xunit (
				tools: [Custom(customXSL: "$WORKSPACE/openjdk-tests/jck/xUnit.xsl",
					deleteOutputFiles: true,
					failIfNotNew: true,
					pattern: "**/TKG/test_output_*/**/report.xml",
					skipNoTestFiles: true,
					stopProcessingIfError: false)]
				)
			}

			if ((currentBuild.result == 'UNSTABLE' || currentBuild.result == 'FAILURE' || currentBuild.result == 'ABORTED') || params.ARCHIVE_TEST_RESULTS) {
				def test_output_tar_name = "${output_name}_test_output${suffix}"
				if (tar_cmd.startsWith('tar')) {
					sh "${tar_cmd} - ${pax_opt} ./openjdk-tests/TKG/test_output_* ${tar_cmd_suffix} > ${test_output_tar_name}"
				} else {
					sh "${tar_cmd} ${test_output_tar_name} ${pax_opt} ./openjdk-tests/TKG/test_output_* ${tar_cmd_suffix}"
				}

				if (!params.ARTIFACTORY_SERVER) {
					echo "ARTIFACTORY_SERVER is not set. Saving artifacts on jenkins."
					archiveArtifacts artifacts: test_output_tar_name, fingerprint: true, allowEmptyArchive: true
				} else {
					def pattern = "${env.WORKSPACE}/*_test_output.*"
					uploadToArtifactory(pattern)
				}
			}
			//for performance test, archive regardless the build result
			if (env.BUILD_LIST.startsWith('perf')) {
				def benchmark_output_tar_name = "benchmark_test_output${suffix}"
				sh "${tar_cmd} ${benchmark_output_tar_name} ${pax_opt} ./openjdk-tests/TKG/test_output_*"
				if (!params.ARTIFACTORY_SERVER) {
					echo "ARTIFACTORY_SERVER is not set. Saving artifacts on jenkins."
					archiveArtifacts artifacts: benchmark_output_tar_name, fingerprint: true, allowEmptyArchive: true
				} else {
					def pattern = "${env.WORKSPACE}/*_test_output.*"
					uploadToArtifactory(pattern)
				}
			}
		}
	}
}

def testBuild() {
	TIME_LIMIT =  params.TIME_LIMIT ? params.TIME_LIMIT.toInteger() : 10
	timeout(time: TIME_LIMIT, unit: 'HOURS') {
		try {
			addJobDescription()
			setup()
			// prepare environment and compile test projects
			if( params.PARALLEL && params.PARALLEL != "None" ) {
				setupParallelEnv()
			} else {
				try {
					//ToDo: temporary workaround for jck test parallel runs
					// until build.xml is added into each subfolder
					if( env.BUILD_LIST.startsWith('jck/')) {
						def temp = env.BUILD_LIST
						env.BUILD_LIST = "jck"
						buildTest()
						env.BUILD_LIST = temp
					} else {
						buildTest()
					}
					runTest()
				} finally {
					post("${env.BUILD_LIST}")
				}
			}
		} finally {
			if (!params.KEEP_WORKSPACE) {
				// cleanWs() does not work in some cases, so set opts below
				cleanWs notFailBuild: true, disableDeferredWipeout: true, deleteDirs: true
				// clean up remaining core files
				if (PLATFORM.contains("mac")) {
					sh "find /cores -name '*core*' -print 2>/dev/null -exec rm -f {} \\; || true"
				} else if (PLATFORM.contains("windows")) {
					sh "find /cygdrive/c/temp -name '*core*' -print 2>/dev/null -exec rm -f {} \\; || true"
				} else {
					sh "find /tmp -name '*core*' -print 2>/dev/null -exec rm -f {} \\; || true"
				}
			}
		}
	}
}

def getJDKImpl(jvm_version) {
	def jdk_impl = 'hotspot'
	if (jvm_version.contains('openj9')) {
		jdk_impl = 'openj9'
	} else if (JVM_VERSION.contains('sap')) {
		jdk_impl = 'sap'
	}
	return jdk_impl
}

def addJobDescription() {
	if (params.PERSONAL_BUILD) {
		// update build name if personal build
		wrap([$class: 'BuildUser']) {
			currentBuild.displayName = "#${BUILD_NUMBER} - ${BUILD_USER_EMAIL}"
		}
	}

	def description = (currentBuild.description) ? currentBuild.description + "<br>" : ""
	currentBuild.description = description \
		+ "TARGET: ${params.TARGET}<br/>" \
		+ "LABEL: <a href=${JENKINS_URL}label/${LABEL}>${LABEL}</a><br/>" \
		+ "<a href=${JENKINS_URL}computer/${NODE_NAME}>${NODE_NAME}</a>"
}

def getJenkinsDomain() {
	String regex = env.JENKINS_URL
	def m = regex =~ /:\/\/(.*)\/?/
	def match = m[0][1]
	def domainName = "undefined"
	if (match) {
		domainName = match
	}
	return domainName
}

def archiveSHAFile() {
	def shaFile = "openjdk-tests/TKG/SHA.txt";
	if (!params.ARTIFACTORY_SERVER) {
		echo "ARTIFACTORY_SERVER is not set. Saving SHA file on jenkins."
		archiveArtifacts artifacts: shaFile, fingerprint: true, allowEmptyArchive: true
	} else {
		def pattern = "${env.WORKSPACE}/${shaFile}"
		uploadToArtifactory(pattern)
	}
}

def uploadToArtifactory(pattern) {
	if (params.ARTIFACTORY_SERVER) {
		def server = Artifactory.server params.ARTIFACTORY_SERVER
		def artifactoryRepo = params.ARTIFACTORY_REPO ? params.ARTIFACTORY_REPO : "sys-rt-generic-local"
		def artifactoryRoorDir = params.ARTIFACTORY_ROOT_DIR ? params.ARTIFACTORY_ROOT_DIR : getJenkinsDomain()
		def artifactoryUploadDir = "${artifactoryRepo}/${artifactoryRoorDir}/${JOB_NAME}/${BUILD_ID}/"

		def uploadSpec = """{
			"files":[
					{
						"pattern": "${pattern}",
						"target": "${artifactoryUploadDir}"
					}
				]
			}"""

		def buildInfo = Artifactory.newBuildInfo()
		// if ARTIFACTORY_NUM_ARTIFACTS is not set, set to keep 20 build artifacts
		def numArtifacts = params.ARTIFACTORY_NUM_ARTIFACTS ? params.ARTIFACTORY_NUM_ARTIFACTS : 20

		buildInfo.retention maxBuilds: numArtifacts, deleteBuildArtifacts: true
		server.upload spec: uploadSpec, buildInfo: buildInfo
		server.publishBuildInfo buildInfo

		def artifactoryUrl = server.getUrl()
		def uploadUrl = "${artifactoryUrl}/${artifactoryUploadDir}"
		echo "Test output artifactory URL:'${uploadUrl}'"
		if (!currentBuild.description.contains(uploadUrl)) {
			currentBuild.description += "<br><a href=${uploadUrl}>Artifacts</a>"
		}
	} else {
		echo "ARTIFACTORY_SERVER is not set. Artifacts are not uploaded onto artifactory server."
	}
}

def addGrinderLink() {
	def	customTargetOpt = params.CUSTOM_TARGET ? "&CUSTOM_TARGET=${params.CUSTOM_TARGET}" :  ""
	def	sdkSourceOpt = params.SDK_RESOURCE ? "&SDK_RESOURCE=${params.SDK_RESOURCE}" :  ""
	def	sdkUrlOpt = params.CUSTOMIZED_SDK_URL ? "&CUSTOMIZED_SDK_URL=${params.CUSTOMIZED_SDK_URL}" : ""
	def	sdkUrlCredentialOpt = params.CUSTOMIZED_SDK_URL_CREDENTIAL_ID ? "&CUSTOMIZED_SDK_URL_CREDENTIAL_ID=${params.CUSTOMIZED_SDK_URL_CREDENTIAL_ID}" : ""
	def	upstreamJobNameOpt = params.UPSTREAM_JOB_NAME ? "&UPSTREAM_JOB_NAME=${params.UPSTREAM_JOB_NAME}" : ""
	def	upstreamJobNumOpt = params.UPSTREAM_JOB_NUMBER ? "&UPSTREAM_JOB_NUMBER=${params.UPSTREAM_JOB_NUMBER}" : ""

	def url = "${HUDSON_URL}/job/Grinder/parambuild/?" \
	+ "JDK_VERSION=${params.JDK_VERSION}" \
	+ "&JDK_IMPL=${params.JDK_IMPL}" \
	+ "&BUILD_LIST=${params.BUILD_LIST}" \
	+ "&PLATFORM=${params.PLATFORM}" \
	+ "&TARGET=${params.TARGET}" \
	+ "${sdkSourceOpt}${sdkUrlOpt}${sdkUrlCredentialOpt}${customTargetOpt}${upstreamJobNameOpt}${upstreamJobNumOpt}"

	url = url.replace(" ", "%20")

	currentBuild.description += "<br><a href=${url}>Rerun in Grinder</a> TARGET can be changed to run only the failed test target"
}

def run_parallel_tests() {
	if (params.PARALLEL && params.PARALLEL != "None") {
		stage ("Parallel Tests") {
			parallel parallel_tests
		}
	}
}

return this
