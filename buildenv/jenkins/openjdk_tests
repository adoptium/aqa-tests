#!groovy

def PLATFORM_MAP = [
    'arm_linux' : [
        'SPEC' : 'linux_arm',
        'LABEL' : 'ci.role.test&&sw.os.linux&&hw.arch.aarch32',
    ],
    'aarch64_mac' : [
        'SPEC' : 'osx_aarch64',
        'LABEL' : 'ci.role.test&&sw.os.osx&&hw.arch.aarch64',
    ],
    'aarch64_linux' : [
        'SPEC' : 'linux_aarch64_cmprssptrs',
        'LABEL' : 'ci.role.test&&sw.os.linux&&hw.arch.aarch64',
    ],
    'aarch64_linux_mixed' : [
        'SPEC' : 'linux_aarch64_mxdptrs',
        'LABEL' : 'ci.role.test&&sw.os.linux&&hw.arch.aarch64',
    ],
    'aarch64_linux_xl' : [
        'SPEC' : 'linux_aarch64',
        'LABEL' : 'ci.role.test&&sw.os.linux&&hw.arch.aarch64',
    ],
    'ppc32_aix' : [
        'SPEC' : 'aix_ppc',
        'LABEL' : 'ci.role.test&&hw.arch.ppc64&&sw.os.aix',
    ],
    'ppc32_linux' : [
        'SPEC' : 'linux_ppc',
        'LABEL' : 'ci.role.test&&hw.arch.ppc64&&sw.os.linux',
    ],
    'ppc64_aix' : [
        'SPEC' : 'aix_ppc-64_cmprssptrs',
        'LABEL' : 'ci.role.test&&hw.arch.ppc64&&sw.os.aix',
    ],
    'ppc64_aix_mixed' : [
        'SPEC' : 'aix_ppc-64_mxdptrs',
        'LABEL' : 'ci.role.test&&hw.arch.ppc64&&sw.os.aix',
    ],
    'ppc64_aix_xl' : [
        'SPEC' : 'aix_ppc-64',
        'LABEL' : 'ci.role.test&&hw.arch.ppc64&&sw.os.aix',
    ],
    'ppc64_linux' : [
        'SPEC' : 'linux_ppc-64_cmprssptrs',
        'LABEL' : 'ci.role.test&&hw.arch.ppc64&&sw.os.linux',
    ],
    'ppc64_linux_xl' : [
        'SPEC' : 'linux_ppc-64',
        'LABEL' : 'ci.role.test&&hw.arch.ppc64&&sw.os.linux',
    ],
    'ppc64le_linux' : [
        'SPEC' : 'linux_ppc-64_cmprssptrs_le',
        'LABEL' : 'ci.role.test&&hw.arch.ppc64le&&sw.os.linux',
    ],
    'ppc64le_linux_mixed' : [
        'SPEC' : 'linux_ppc-64_mxdptrs_le',
        'LABEL' : 'ci.role.test&&hw.arch.ppc64le&&sw.os.linux'
    ],
    'ppc64le_linux_xl' : [
        'SPEC' : 'linux_ppc-64_le',
        'LABEL' : 'ci.role.test&&hw.arch.ppc64le&&sw.os.linux',
    ],
    'riscv64_linux' : [
        'SPEC' : 'linux_riscv64_cmprssptrs',
        'LABEL' : 'ci.role.test&&sw.os.linux&&hw.arch.riscv&&hw.bits.64',
    ],
    'riscv64_linux_xl' : [
        'SPEC' : 'linux_riscv64',
        'LABEL' : 'ci.role.test&&sw.os.linux&&hw.arch.riscv&&hw.bits.64',
    ],
    's390_linux' : [
        'SPEC' : 'linux_390',
        'LABEL' : 'ci.role.test&&hw.arch.s390x&&sw.os.linux&&hw.bits.32',
        'DynamicAgents' : ['fyre']
    ],
    's390x_linux' : [
        'SPEC' : 'linux_390-64_cmprssptrs',
        'LABEL' : 'ci.role.test&&hw.arch.s390x&&sw.os.linux',
        'DynamicAgents' : ['fyre']
    ],
    's390x_linux_mixed' : [
        'SPEC' : 'linux_390-64_mxdptrs',
        'LABEL' : 'ci.role.test&&hw.arch.s390x&&sw.os.linux',
        'DynamicAgents' : ['fyre']
    ],
    's390x_linux_xl' : [
        'SPEC' : 'linux_390-64',
        'LABEL' : 'ci.role.test&&hw.arch.s390x&&sw.os.linux',
        'DynamicAgents' : ['fyre']
    ],
    's390x_zos' : [
        'SPEC' : 'zos_390-64_cmprssptrs',
        'LABEL' : 'ci.role.test&&hw.arch.s390x&&sw.os.zos',
    ],
    's390x_zos_xl' : [
        'SPEC' : 'zos_390-64',
        'LABEL' : 'ci.role.test&&hw.arch.s390x&&sw.os.zos',
    ],
    's390_zos' : [
        'SPEC' : 'zos_390',
        'LABEL' : 'ci.role.test&&hw.arch.s390x&&sw.os.zos',
    ],
    'sparcv9_solaris' : [
        'SPEC' : 'sunos_sparcv9-64_cmprssptrs',
        'LABEL' : 'ci.role.test&&hw.arch.sparcv9&&sw.os.sunos',
    ],
    'x86-64_solaris' : [
        'SPEC' : 'sunos_x86-64_cmprssptrs',
        'LABEL' : 'ci.role.test&&hw.arch.x86&&sw.os.sunos',
    ],
    'x86-64_alpine-linux' : [
        'SPEC' : 'alpine-linux',
        'LABEL' : 'ci.role.test&&hw.arch.x86&&sw.os.alpine-linux',
    ],
    'x86-32_linux' : [
        'SPEC' : 'linux_x86',
        'LABEL' : 'ci.role.test&&hw.arch.x86&&sw.os.linux',
    ],
    'x86-32_windows' : [
        'SPEC' : 'win_x86',
        'LABEL' : 'ci.role.test&&hw.arch.x86&&sw.os.windows',
    ],
    'x86-64_linux' : [
        'SPEC' : 'linux_x86-64_cmprssptrs',
        'LABEL' : 'ci.role.test&&hw.arch.x86&&sw.os.linux',
        'DynamicAgents' : ['azure', 'fyre']
    ],
    'x86-64_linux_mixed' : [
        'SPEC' : 'linux_x86-64_mxdptrs',
        'LABEL' : 'ci.role.test&&hw.arch.x86&&sw.os.linux',
        'DynamicAgents' : ['azure', 'fyre']
    ],
    'x86-64_linux_xl' : [
        'SPEC' : 'linux_x86-64',
        'LABEL' : 'ci.role.test&&hw.arch.x86&&sw.os.linux',
        'DynamicAgents' : ['azure', 'fyre']
    ],
    'x86-64_mac' : [
        'SPEC' : 'osx_x86-64_cmprssptrs',
        'LABEL' : 'ci.role.test&&hw.arch.x86&&sw.os.osx',
    ],
    'x86-64_mac_mixed' : [
        'SPEC' : 'osx_x86-64_mxdptrs',
        'LABEL' : 'ci.role.test&&hw.arch.x86&&sw.os.osx',
    ],
    'x86-64_mac_xl' : [
        'SPEC' : 'osx_x86-64',
        'LABEL' : 'ci.role.test&&hw.arch.x86&&sw.os.osx',
    ],
    'x86-64_windows' : [
        'SPEC' : 'win_x86-64_cmprssptrs',
        'LABEL' : 'ci.role.test&&hw.arch.x86&&sw.os.windows',
    ],
    'x86-64_windows_mixed' : [
        'SPEC' : 'win_x86-64_mxdptrs',
        'LABEL' : 'ci.role.test&&hw.arch.x86&&sw.os.windows',
    ],
    'x86-64_windows_xl' : [
        'SPEC' : 'win_x86-64',
        'LABEL' : 'ci.role.test&&hw.arch.x86&&sw.os.windows',
    ],
]

if ( !params.TARGET ) {
    assert false : "Please provide TARGET value"
}
def PLATFORMS = params.PLATFORM.trim().split("\\s*,\\s*");
def JDK_VERSIONS = params.JDK_VERSION.trim().split("\\s*,\\s*");
def JDK_IMPLS = params.JDK_IMPL.trim().split("\\s*,\\s*");

// if multiple JDK_VERSION / JDK_IMPL / PLATFORM are provided, run test jobs in parallel
if (JDK_VERSIONS.size() > 1 || JDK_IMPLS.size() > 1 || PLATFORMS.size() >1 || PLATFORMS.contains("all")) {
    if (SDK_RESOURCE != 'nightly' && SDK_RESOURCE != 'releases') {
        assert false : "Multiple Grinders should run with SDK_RESOURCE=nightly or releases."
    } else {
        testJobs = [:]
        PLATFORMS.each { PLATFORM ->
            JDK_VERSIONS.each { JDK_VERSION ->
                JDK_IMPLS.each { JDK_IMPL ->
                    def ACTUAL_PLATFORM = PLATFORM
                    if (PLATFORM == "all") {
                        ACTUAL_PLATFORM = "ppc64_aix,ppc64le_linux,s390x_linux,x86-64_linux,x86-64_mac,x86-64_windows"
                        if (JDK_IMPL == "openj9" || JDK_IMPL == "ibm"){
                            ACTUAL_PLATFORM = ACTUAL_PLATFORM + ",ppc64_aix_xl,ppc64le_linux_xl,s390x_linux_xl,x86-64_linux_xl,x86-64_mac_xl,x86-64_windows_xl"
                        }
                        if (JDK_VERSION == "8"){
                            ACTUAL_PLATFORM = ACTUAL_PLATFORM + ",x86-32_windows"
                        } else {
                            ACTUAL_PLATFORM = ACTUAL_PLATFORM + ",aarch64_linux"
                        }
                    }
                    def childParams = []
                    // loop through all the params and change the parameters if needed
                    params.each { param ->
                        if (param.key == "PLATFORM") {
                            childParams << string(name: param.key, value: ACTUAL_PLATFORM)
                        } else if (param.key == "JDK_VERSION") {
                            childParams << string(name: param.key, value: JDK_VERSION)
                        } else if (param.key == "JDK_IMPL") {
                            childParams << string(name: param.key, value: JDK_IMPL)
                        } else {
                            def value = param.value.toString()
                            if (value == "true" || value == "false") {
                                childParams << booleanParam(name: param.key, value: value.toBoolean())
                            } else {
                                childParams << string(name: param.key, value: value)
                            }
                        }
                    }
                    testJobs["openjdk${JDK_VERSION}_${JDK_IMPL}_${PLATFORM}"] = {
                        build job: JOB_NAME, parameters: childParams
                    }
                }
            }
        }
        parallel testJobs
    }
} else {
    if (PLATFORM_MAP.containsKey(params.PLATFORM)) {
        SPEC = PLATFORM_MAP[params.PLATFORM]["SPEC"]
        LABEL = params.LABEL ? params.LABEL : PLATFORM_MAP[params.PLATFORM]["LABEL"]

        if (params.DOCKER_REQUIRED) {
            LABEL += "&&sw.tool.docker"
        }

        if (params.LABEL_ADDITION) {
            LABEL += "&&${params.LABEL_ADDITION}"
        }

        println "SPEC: ${SPEC}"
        println "LABEL: ${LABEL}"

        dynamicAgents = []
        if (LABEL == PLATFORM_MAP[params.PLATFORM]["LABEL"] && PLATFORM_MAP[params.PLATFORM].containsKey('DynamicAgents')) {
            dynamicAgents = PLATFORM_MAP[params.PLATFORM]["DynamicAgents"]
        }

        stage('Queue') {
            String[] onlineNodes = nodesByLabel(LABEL)
            if (onlineNodes.size() < 1) {
                int ACTIVE_NODE_TIMEOUT = params.ACTIVE_NODE_TIMEOUT ? params.ACTIVE_NODE_TIMEOUT : 0
                timeout(ACTIVE_NODE_TIMEOUT) {
                    // If there is available node before timeout
                    node(LABEL) {
                        echo "find the node with label as ${env.NODE_NAME}"
                    }
                }
            } else {
                // IF no nodes are idle we will check if there is supported virtual agent
                // When Parallel the race condition could happen. Say the number of multiply jobs is larger than the available nodes the query's result may be delayed and wrong
                // In this case jobs will be fooled to fall back to wait local busy nodes. 
                if (params.CLOUD_PROVIDER != null && params.CLOUD_PROVIDER in dynamicAgents) {
                    boolean isNodeIdle = false
                    node {
                        for (String onlineNode : onlineNodes) {
                            def nodeStatus = sh(script: "curl -s ${env.JENKINS_URL}computer/${onlineNode}/api/xml?xpath=/*/idle",
                                                returnStdout: true,
                                                returnStatus: false
                                                ).trim()
                            if (nodeStatus.contains("true")) {
                                isNodeIdle = true
                                break
                            }
                        }
                    }
                    if (!isNodeIdle) {
                       LABEL = LABEL.minus("ci.role.test&&")
                       LABEL += '&&ci.agent.dynamic'
                    }
                }
            }

            node(LABEL) {
                cleanWs()
                if (params.PLATFORM.contains('zos')) {
                    /* Ensure correct CC env */
                    env._CC_CCMODE = '1'
                    env._CXX_CCMODE = '1'
                    env._C89_CCMODE = '1'

                    def gitConfig = scm.getUserRemoteConfigs()[0]
                    def SCM_GIT_REPO = gitConfig.getUrl()
                    def SCM_GIT_BRANCH = scm.branches[0].name

                    // SCM_GIT_REPO value only gets expanded in sh
                    def SCM_GIT_REPO_VAL = sh(script: "echo ${SCM_GIT_REPO}", returnStdout: true).trim()
                    SCM_GIT_REPO_VAL = SCM_GIT_REPO_VAL.replace("https://github.com/","git@github.com:")

                    sh "git clone -b ${SCM_GIT_BRANCH} ${SCM_GIT_REPO_VAL} openjdk-tests"
                } else {
                    def gitConfig = scm.getUserRemoteConfigs().get(0)

                    // Adopt windows machines require env here https://github.com/adoptium/aqa-tests/issues/1803
                    ref_cache = "${env.HOME}/openjdk_cache"

                    checkout scm: [$class: 'GitSCM',
                        branches: [[name: "${scm.branches[0].name}"]],
                        extensions: [
                            [$class: 'CleanBeforeCheckout'],
                            [$class: 'CloneOption', reference: ref_cache],
                            [$class: 'RelativeTargetDirectory', relativeTargetDir: 'openjdk-tests']],
                        userRemoteConfigs: [[url: "${gitConfig.getUrl()}"]]
                    ]
                }
                jenkinsfile = load "${WORKSPACE}/openjdk-tests/buildenv/jenkins/JenkinsfileBase"
                if (LABEL.contains('ci.agent.dynamic') && CLOUD_PROVIDER == 'azure') {
                    //Set dockerimage for azure agent. Fyre has stencil to setup the right environment
                    docker.image('adoptopenjdk/centos6_build_image').pull() 
                    docker.image('adoptopenjdk/centos6_build_image').inside {
                        jenkinsfile.testBuild()
                    }
                } else {
                    jenkinsfile.testBuild()
                }
            }
        }
        jenkinsfile.run_parallel_tests()
    } else {
        assert false : "Cannot find key PLATFORM: ${params.PLATFORM} in PLATFORM_MAP: ${PLATFORM_MAP}."
    }
}
