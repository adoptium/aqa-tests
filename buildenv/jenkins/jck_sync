#!groovy

timestamps{
    stage('Setup') {
        def SYNC_LABEL = params.SYNC_LABEL ?: "ci.role.test&&hw.arch.x86&&sw.os.linux"
        println "SYNC_LABEL: ${SYNC_LABEL}"
        node(SYNC_LABEL) {
            echo ("clone the SCM GIT repo")
            try {
                def gitConfig = scm.getUserRemoteConfigs().get(0)
                timeout(time: 1, unit: 'HOURS') {
                    forceCleanWS()
                }
                checkout scm: [$class: 'GitSCM',
                branches: [[name: "${scm.branches[0].name}"]],
                extensions: [
                    [$class: 'CleanBeforeCheckout'],
                    [$class: 'CloneOption'],
                    [$class: 'RelativeTargetDirectory', relativeTargetDir: 'aqa-tests']],
                userRemoteConfigs: [[url: "${gitConfig.getUrl()}"]]
                ]
                syncJckMaterial()
            } catch (Exception e) {
                // build result may not be updated correctly at the moment (see https://issues.jenkins.io/browse/JENKINS-56402)
                // if there is an exception, set currentBuild.result to ABORTED/FAILURE
                if (e.toString().contains("The job is completed without further action")){
                    println("JCKUpdater script returned exit code 2, as no new update available. The job is completed without further action.")
                    currentBuild.result = "SUCCESS"
                } else if (e.toString().contains("FlowInterruptedException")) {
                    currentBuild.result = 'ABORTED'
                    println("Exception: " + e.toString())
                } else {
                    currentBuild.result = 'FAILURE'
                    println("Exception: " + e.toString())
                }
            }
        }
    }
}

def forceCleanWS() {
    try {
        cleanWs disableDeferredWipeout: true, deleteDirs: true
    } catch (Exception e) {
        echo 'Exception: ' + e.toString()
        //cleanWs has issue to delete workspace that contains non-ASCII filename in TKG output https://issues.jenkins.io/browse/JENKINS-33478
        //cannot delete workspace directly. Otherwise, Jenkins job will abort due to missing workspace
        sh "rm -rf ${env.WORKSPACE}/aqa-tests/TKG"
        // call cleanWs() again
        cleanWs disableDeferredWipeout: true, deleteDirs: true
    }
}

def syncJckMaterial(){
    echo ("Starting script to sync JCK materials .....")
    def JCK_VERSION_OPTION = params.JDK_VERSION ? "-j ${params.JDK_VERSION}": ""
    def JCK_GIT_BRANCH_OPTION = params.JCK_GIT_BRANCH ? "-gb ${params.JCK_GIT_BRANCH}": ""
    def ARTIFACTORY_URL_OPTION = params.ARTIFACTORY_URL ? "-au ${params.ARTIFACTORY_URL}": ""
    def JCK_GIT_REPO_OPTION = params.JCK_GIT_REPO ? "-repo ${params.JCK_GIT_REPO}": ""
    def PR_NUMBER = 0
    stage('JCKUpdater') {
        getJavaSDK()

        withCredentials([
            usernamePassword(credentialsId: "${params.ARTIFACTORY_CREDENTIALS}",
			usernameVariable: 'ARTIFACTORY_USER', passwordVariable: 'ARTIFACTORY_CREDENTIALS'),
            string(credentialsId: "${params.GIT_CREDENTIALS}",variable: 'GIT_CREDENTIALS')
        ]) {
            dir("${WORKSPACE}/aqa-tests/jck/jck-semiauto-updater"){
                sshagent(credentials:["${params.USER_CREDENTIALS_ID}"], ignoreMissing: true){
                    def SCRIPT = "./jckupdater.sh ${JCK_VERSION_OPTION} -at  \"\$ARTIFACTORY_CREDENTIALS\" ${JCK_GIT_REPO_OPTION}  -gt \"\$GIT_CREDENTIALS\"  ${ARTIFACTORY_URL_OPTION}  ${JCK_GIT_BRANCH_OPTION} "
                    def tempOutputFile = "temp_output.txt"
                    def EXITCODE = sh(script: "${SCRIPT} > ${tempOutputFile}",  returnStatus: true)
                    if (EXITCODE == 2) {
                        error ("JCKUpdater script returned exit code 2, as no new update available. The job is completed without further action.")
                        currentBuild.result = "SUCCESS"
                    } else if (EXITCODE == 0) {
                        echo ("Script returned exit code 0, new update is available and PR is created.")
                        def scriptOutput = readFile(tempOutputFile).trim()
                        echo ("JCKUpdater script output:")
                        echo scriptOutput
                        def matcher = (scriptOutput =~ /PR_NUMBER=\s*(\d+)/)
                        PR_NUMBER = matcher ? matcher[0][1] : null
                        if (PR_NUMBER == null || PR_NUMBER == 0) {
                            error ("PR_NUMBER not found in script output.")
                        } else {
                            echo ("PR created with number : ${PR_NUMBER}")
                        }
                    } else {
                        error ("script failed with exit code ${EXITCODE}")
                    }
                }
            }
        }
    }

    stage('Scripts'){
        def CHILD_JOB_NAME="JCK_Scripts"
        def buildURL="${JENKINS_URL}job/${CHILD_JOB_NAME}/"
        def childParams = []
        params.each { param ->
            def value = param.value.toString()
            if (value == "true" || value == "false") {
                childParams << booleanParam(name: param.key, value: value.toBoolean())
            } else {
                childParams << string(name: param.key, value: value)
            }
        }
        childParams << string(name: 'PR_NUMBER', value: PR_NUMBER)
        def buildInfo = build job: CHILD_JOB_NAME, parameters: childParams
        def comment="Job [#${buildInfo.number}](${buildURL}${buildInfo.number}) completed with status: ${buildInfo.result}\\r\\n"
        createComment(comment,PR_NUMBER)
    }

    stage('Test') {
        def CHILD_JOB_NAME="Grinder_JCK"
        def buildURL="${JENKINS_URL}job/${CHILD_JOB_NAME}/"
        def childParams = []
        // loop through all the params and change the parameters if needed
        params.each { param ->
            def value = param.value.toString()
            if (param.key == "JCK_GIT_BRANCH") {
                if (value.trim() == ""){
                    value = "autoBranch" //default branch name given in JCKUpdater.sh script
                }
                childParams << string(name: param.key, value: value)
            } else {
                if (value == "true" || value == "false") {
                    childParams << booleanParam(name: param.key, value: value.toBoolean())
                } else {
                    childParams << string(name: param.key, value: value)
                }
            }
        }
        def buildInfo = build job: CHILD_JOB_NAME, parameters: childParams
        def comment="Job [#${buildInfo.number}](${buildURL}${buildInfo.number}) completed with status: ${buildInfo.result}"
        createComment(comment,PR_NUMBER)
    }
}

//Download JAVA SDK 
def getJavaSDK(){
    def JDK_VERSION_OPTION = params.JDK_VERSION ? "-j ${params.JDK_VERSION}" : ""
    def CUSTOMIZED_SDK_URL_OPTION = "-c ${params.CUSTOMIZED_SDK_URL}"
    def CLONE_OPENJ9_OPTION = "--clone_openj9 false"
    def PLATFORM_OPTION = "-p x86-64_linux"
    def GET_SH_CMD = "./get.sh -s `pwd`/.. ${PLATFORM_OPTION} ${JDK_VERSION_OPTION} ${CUSTOMIZED_SDK_URL_OPTION} ${CLONE_OPENJ9_OPTION} "
    dir("${WORKSPACE}/aqa-tests") {
        if (params.CUSTOMIZED_SDK_URL_CREDENTIAL_ID) {
			withCredentials([usernamePassword(credentialsId: "${params.CUSTOMIZED_SDK_URL_CREDENTIAL_ID}", usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD')]) {
			sh "$GET_SH_CMD"
			}
		} else {
			sh "$GET_SH_CMD"
		}
    }
}

def createComment(comment,number) {
    withCredentials([
        string(credentialsId: "${params.GIT_CREDENTIALS}",variable: 'GIT_CREDENTIALS')
    ]) {
        def DOMAIN = params.JCK_GIT_REPO.split(/[@:]/)
        def REPO = DOMAIN[2].split('\\.')
        def COMMENT_URL="https://${DOMAIN[1]}/api/v3/repos/${REPO[0]}/issues/${number}/comments"
        def PR_URL="https://${DOMAIN[1]}/${REPO[0]}/pull/${number}"
        def CURL_COMMAND = """
            curl -X POST -H "Authorization: token \"\$GIT_CREDENTIALS\"" \\
            -d '{
            "body":"${comment}"
            }' "$COMMENT_URL"
        """
        def STATUS = sh(script: CURL_COMMAND, returnStatus: true, returnStdout: true)
        if (STATUS == 0) {
            echo ("Comment added successfully to PR ${PR_URL} ")
        } else {
            error ("Failed to add a comment to PR ${PR_URL} ")
        }
    }
}
